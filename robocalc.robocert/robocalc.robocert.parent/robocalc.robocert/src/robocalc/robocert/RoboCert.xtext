/**
 * The RoboCert grammar.
 *
 * Where possible, we try to keep this grammar's layout in line with that of
 * the Emfatic source of the RoboCert metamodel.
 */
grammar robocalc.robocert.RoboCert with circus.robocalc.robochart.textual.RoboChart

import "http://robocert.robocalc/"
import "http://www.robocalc.circus/RoboChart"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

///////////////////////////////////////////////////////////////////////////////
// CORE LANGUAGE
///////////////////////////////////////////////////////////////////////////////
/**
 * A top-level RoboCert package.
 */
CertPackage:
	(groups+=(CSPGroup | SpecificationGroup | AssertionGroup))*;

//
// TARGETS
//

/**
 * A target of a sequence diagram.
 */
Target:
	ComponentTarget | CollectionTarget;

ComponentTarget:
	'module' {ModuleTarget} module=[RCModule|QualifiedName] |
	'controller' {ControllerTarget} controller=[ControllerDef|QualifiedName] |
	'state' 'machine' {StateMachineTarget} stateMachine=[StateMachineDef|QualifiedName] |
	'operation' {OperationTarget} operation=[OperationDef|QualifiedName];

CollectionTarget:
	'components' 'of' (
		'module' {InModuleTarget} module=[RCModule|QualifiedName] |
		'controller' {InControllerTarget} controller=[ControllerDef|QualifiedName]
	);

// TODO(@MattWindsor91): extended targets

/**
 * A constant assignment.
 * 
 * As with many such lists, the definition of ConstAssignment allows commas
 * with an optional trailing (potentially Oxford-style) 'and'.
 */
ConstAssignment:
	ConstName (',' ConstName)* (','? 'and' ConstName)? AssignWords value=Expression;

/**
 * Adds a constant to the constants list.
 */
fragment ConstName *:
	constants+=[Variable|QualifiedName];

/**
 * Various synonyms for assignment.
 */
fragment AssignWords *:
	'set' 'to' | 'assigned';

/* RoboCert is whitespace-sensitive, with these terminals delimiting blocks
 * where a change of indentation must happen.
 * 
 * See:
 * https://www.eclipse.org/Xtext/documentation/307_special_languages.html#whitespace-aware-languages
 */
terminal BEGIN:
	'synthetic:BEGIN';

terminal END:
	'synthetic:END';

/**
 * A specification group.
 * 
 * Specification groups contain specifications alongside other elements such as
 * actors, message sets, and so on.
 */
SpecificationGroup:
	'specification' 'group' {SpecificationGroup} name=ID?
	BEGIN
	'target' '=' target=Target ('with' BEGIN (assignments+=ConstAssignment)+ END)?
	(ActorList | interactions+=Interaction | messageSets+=NamedMessageSet)*
	END;

/**
 * Explicitly specifying the semantic model of a property.
 * 
 * TODO(@MattWindsor91): there may need to be more abstract ways of specifying
 * these models eventually.
 */
fragment InSemanticModelSpec *:
	'in' 'the' model=SemanticModel 'model';

/**
 * Names of semantic models.
 */
enum SemanticModel:
	TRACES='traces' |
	TIMED='timed';


//
// ASSERTIONS
//
/**
 * An assertion group.
 * 
 * As syntactic sugar, unnamed assertion groups with only one assertion may be
 * written as if they were just lone Assertions.
 */
AssertionGroup:
	'assertion' (SingletonAssertionGroup | FullAssertionGroup);

/**
 * Produces the body of a singleton assertion group.
 * 
 * The instantiation of such a group appears at the end of the assertion.
 */
SingletonAssertionGroup returns AssertionGroup:
	assertions+=Assertion;

/**
 * Produces the body of a non-singleton assertion group.
 */
FullAssertionGroup returns AssertionGroup:
	{AssertionGroup} 'group' (name=ID)? BEGIN ('assertion' assertions+=Assertion)* END;

/**
 * An assertion, less the leading 'assertion' keyword (because this might be
 * the end of a singleton AssertionGroup).
 * 
 * Assertions may break over whitespace, for readability.
 */
Assertion:
	name=ID ':' (property=Property | BEGIN property=Property END);

/**
 * Top-level disjunction of properties.
 */
Property:
	CoreProperty | CSPProperty | SequenceProperty;

/**
 * Core properties with only one argument.
 */
CoreProperty:
	'target' 'of' group=[SpecificationGroup|QualifiedName]
	('is' (negated?='not')? type=IsCorePropertyType |
	type=VerbCorePropertyType |
	'does' (negated?='not')? type=DoesCorePropertyType);

// This factoring of an enumeration into 'is', 'does', and 'does not' is common
// in the RoboCert assertions language.
/**
 * Unary core property types that agree with 'is'/'is not'.
 */
enum IsCorePropertyType returns CorePropertyType:
	DEADLOCK_FREE='deadlock-free' |
	DETERMINISM='deterministic' |
	TIMELOCK_FREE='timelock-free';

/**
 * Unary core property types that are standalone verbs in the third
 * person present tense.
 */
enum VerbCorePropertyType returns CorePropertyType:
	// Remember to add these to DoesNot also.
	TERMINATION='terminates';

/**
 * Unary core property types that agree with 'does'/'does not'.
 */
enum DoesCorePropertyType returns CorePropertyType:
	// Remember to add these to Verb also.
	TERMINATION='terminate';

/**
 * A time unit expression.
 */
TimeUnits returns Expression:
	Expression 'time'? ('unit' | 'units');

//
// MODALITIES
//
/**
 * Captures an optional temperature with no other related side-material.
 */
fragment OptTemperature *:
	('(' temperature=Temperature ')')?;

/**
 * A temperature specification.
 */
enum Temperature:
	COLD='cold' |
	HOT='hot';


///////////////////////////////////////////////////////////////////////////////
// LOW-LEVEL LANGUAGES
///////////////////////////////////////////////////////////////////////////////


/**
 * Interpolation of low-level languages.
 */
terminal INTERPOLATE:
	'<$'->'$>';

//
// CSP
//

/**
 * A CSP fragment in group position.
 */
CSPGroup:
	'csp' {CSPGroup} name=ID? csp=INTERPOLATE
;

/**
 * A CSP property in assertion position.
 */
CSPProperty:
    'csp' {CSPProperty} (negated?='negated') csp=INTERPOLATE
;

///////////////////////////////////////////////////////////////////////////////
// SEQUENCES
///////////////////////////////////////////////////////////////////////////////


fragment ActorList *:
	// should match 'use X', 'use X, Y', 'use X and Y', 'use X, Y and Z',
	// 'use X, Y, and Z', and so on.
	'actors' '=' '{' actors+=Actor (',' actors+=Actor)* (','? 'and' actors+=Actor)? '}'
;

/**
 * A sequence diagram.
 */
Interaction:
	'sequence' name=ID
	BEGIN
	(variables=InteractionVars)?	
	(('actor' | 'actors') actors+=[Actor] (',' actors+=[Actor])* (','? 'and' actors+=[Actor])?)+
	fragments+=InteractionFragment+
	END;

/**
 * An interaction variable list (may be empty).
 */
InteractionVars returns VariableList:
	{VariableList}
	modifier=InteractionVarsModifier vars+=Variable (',' vars+=Variable)* (','? 'and' vars+=Variable)?
;

/**
 * Subset of variable modifiers permitted on an interaction.
 */
enum InteractionVarsModifier returns VariableModifier:
	// Ill-formed to use const here
	VAR='var'
;

/**
 * A subsequence.
 */
fragment Subsequence *:
	BEGIN fragments+=InteractionFragment+ END;

/**
 * An interaction operand in block position.
 *
 * These can be guarded or not guarded, and have:
 * - a fully indented subsequence, ended with 'end';
 * - a single item, which needn't be indented;
 * - 'nothing'.
 */
BlockInteractionOperand returns InteractionOperand:
	// Singleton
	':' guard=Guard (fragments+=InteractionFragment | 'nothing') |
	// Non-singleton
	guard=Guard (Subsequence 'end' | 'nothing')
;

/**
 * An interaction operand in branch position.
 *
 * As per BlockInteractionOperand, but these are always guarded, and do not have a trailing 'end'.
 * (The 'end' comes from the enclosing branch.)
 */
BranchInteractionOperand returns InteractionOperand:
	// Singleton
	':' guard=Guard (fragments+=InteractionFragment | 'nothing') |
	// Non-singleton
	guard=Guard (Subsequence | 'nothing')
;

/**
 * An explicit guard.
 */
Guard:
	{EmptyGuard} |
	'[' (
	'always' {EmptyGuard} |
	'otherwise' {ElseGuard} |
	{ExprGuard} expr=Expression
	) ']';

/**
 * Consumes nothing and instantly returns an empty guard.
 */
ImplicitEmptyGuard returns EmptyGuard:
	{EmptyGuard}
;

//
// INTERACTION FRAGMENTS
//
/**
 * A step in a sequence diagram.
 */
InteractionFragment:
	BlockFragment |
	BranchFragment |
	{OccurrenceFragment} occurrence=Occurrence OptTemperature;

//
// Block fragments
//
/**
 * A block fragment.
 */
BlockFragment:
	(DurationFragment |
	('opt' | 'optional') {OptFragment} |
	('any' | 'anything') {UntilFragment} intraMessages=IntraMessageSet 'until' |
	'block' {UntilFragment} intraMessages=EmptySet |
	'loop' {LoopFragment} ('(' bound=DiscreteBound 'times'? ')')?) (name=ID)? body=BlockInteractionOperand;

DurationFragment:
	'duration' '(' bound=DiscreteBound 'units'? ')' 'on' actor=[Actor];

//
// Discrete bounds
//

/**
 * A discrete bound.
 */
DiscreteBound:
	upper=Expression |
	'at' 'least' lower=Expression |
	'between' lower=Expression 'and' upper=Expression;

//
// Branch steps
//
/**
 * A branching construct.
 */
BranchFragment:
	ParFragment | AltOrXAltFragment;

ParFragment:
	('par' | 'parallel') {ParFragment} (name=ID)? 
		branches+=BranchInteractionOperand
		('and' branches+=BranchInteractionOperand)+
	'end';
	
AltOrXAltFragment returns BranchFragment:
	(('alt' | 'provisional'? 'alternative') {AltFragment} | ('xalt' | 'mandatory' 'alternative') {XAltFragment}) (name=ID)?
		branches+=BranchInteractionOperand
		('else' branches+=BranchInteractionOperand)+
	'end';

//
// OCCURRENCES
//
/**
 * An occurrence in a sequence diagram.
 */
Occurrence:
	{MessageOccurrence} message=Message |
	LifelineOccurrence;

LifelineOccurrence:
	('after' {WaitOccurrence} units=TimeUnits |
	'deadlock' {DeadlockOccurrence}) 'on' actor=[Actor|QualifiedName];

//
// MESSAGES
//
/**
 * A message specification.
 */
Message:
	from=[Actor|ID] '->>' to=[Actor|ID] ':' topic=MessageTopic ('(' (arguments+=ValueSpecification (',' arguments+=ValueSpecification)*)? ')')?;

//
// Message topics
//
/**
 * A message topic.
 */
MessageTopic:
	'event' {EventTopic} efrom=[Event|QualifiedName] ('/' eto=[Event|QualifiedName]) |
	('op' | 'operation') {OperationTopic} operation=[OperationSig|QualifiedName];

//
// Message sets
//
/**
 * A named message set.
 */
NamedMessageSet:
	'message' 'set' name=ID '=' set=MessageSet;

/**
 * The topmost level of an intra-message set specification, one of:
 * 
 * - 'in X';
 * - 'in X except Y';
 * - 'except Y';
 * - '' (universe implied).
 */
IntraMessageSet returns MessageSet:
	'in' MessageSet |
	{UniverseMessageSet} ({BinaryMessageSet.lhs=current} operator=DiffOperator rhs=UnionOrInterSet)?;

/**
 * The main top-level rule for message sets, after accounting for the
 * possible omission of 'in X' in an 'anything except Y' stanza.
 */
MessageSet:
	UnionOrInterSet ({BinaryMessageSet.lhs=current} (operator=DiffOperator) rhs=UnionOrInterSet)*;

/**
 * Factored-out level for union or intersection sets.
 */
UnionOrInterSet returns MessageSet:
	PrimitiveSet ({BinaryMessageSet.lhs=current} (operator=UnionOrInterOperator) rhs=PrimitiveSet)*;

/**
 * Union or intersection operator literals.
 */
enum UnionOrInterOperator returns BinarySetOperator:
	UNION='or' |
	INTERSECTION='and';

/**
 * Difference operator literal.
 */
enum DiffOperator returns BinarySetOperator:
	DIFFERENCE='except';

/**
 * Pseudo-rule that pulls a gap message set to empty if we don't see an
 * explicit rule.
 */
EmptySet returns ExtensionalMessageSet:
	{ExtensionalMessageSet};

/**
 * A primitive message set.
 */
PrimitiveSet returns MessageSet:
	'(' MessageSet ')' |
	'universe' {UniverseMessageSet} |
	ExtensionalMessageSet |
	'message'? 'set' {RefMessageSet} set=[NamedMessageSet];

/**
 * An extensional message set (including singleton message sets).
 */
ExtensionalMessageSet:
	'{' {ExtensionalMessageSet} messages+=Message (',' messages+=Message)* '}' |
	messages+=Message;

//
// Value specifications
//
/**
 * A value specification.
 */
ValueSpecification:
	{ExpressionValueSpecification} expr=Expression |
	{WildcardValueSpecification} ('any' | 'anything') ('into' destination=[Variable])?;

//
// ACTORS
//
Actor:
	('target' {TargetActor} | 'world' {World} | 'component' {ComponentActor} node=[ConnectionNode|QualifiedName]) 'as' name=ID;

//
// SEQUENCE PROPERTIES
//
/**
 * A sequence property.
 * 
 * These are:
 * 
 * - SEQUENCE is observed (in CSP: sequence refines target)
 * - SEQUENCE is not observed (in CSP: sequence does not refine target)
 * - SEQUENCE holds (in CSP: target refines sequence)
 * - SEQUENCE does not hold (in CSP: target does not refine sequence)
 */
SequenceProperty:
	interaction=[Interaction|QualifiedName]
	('is' negated?='not' type=IsSequencePropertyType
	| 'does' negated?='not' type=DoesNotSequencePropertyType
	| type=DoesSequencePropertyType)
	InSemanticModelSpec?;

/**
 * Sequence property types that agree with 'is' and 'is not'.
 */
enum IsSequencePropertyType returns SequencePropertyType:
	IS_OBSERVED='observed';

/**
 * Sequence property types that agree with 'does not'.
 */
enum DoesNotSequencePropertyType returns SequencePropertyType:
	HOLDS='hold';

/**
 * Sequence property that agree without a prefix (eg, the opposite of
 * 'does not' sequence property types).
 */
enum DoesSequencePropertyType returns SequencePropertyType:
	HOLDS='holds';
