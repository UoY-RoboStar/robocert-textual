package robocalc.robocert.generator.tockcsp.top

import com.google.inject.Inject
import org.eclipse.emf.ecore.resource.Resource
import robocalc.robocert.generator.tockcsp.seq.SequenceGenerator
import robocalc.robocert.model.robocert.Assertion
import robocalc.robocert.model.robocert.CSPGroup
import robocalc.robocert.model.robocert.SequenceGroup
import java.util.Date
import java.text.SimpleDateFormat
import robocalc.robocert.generator.utils.UnsupportedSubclassHandler
import robocalc.robocert.model.robocert.Group

/**
 * Top-level generator for tock-CSP.
 */
class TopGenerator {
	@Inject extension AssertionGenerator
	@Inject extension CSPGroupGenerator
	@Inject extension ImportGenerator
	@Inject extension SequenceGenerator sg
	@Inject extension UnsupportedSubclassHandler
	
	/**
	 * @return generated CSP for all elements.
	 * 
	 * @param resource  the top-level property model.
	 */
	def CharSequence generate(Resource resource) '''
		«generateHeader»

		«resource.generatePreambleCSPGroups»

		«resource.generateImports»

		--
		-- Specifications
		--
		
		«resource.generateGroups»
		
		--
		-- Assertions
		--
		
		«resource.generateAssertions»
	'''
	
	/**
	 * Similar to CUntimedGenerator in RoboChart, and should probably include
	 * the version eventually too.
	 * 
	 * @return the generated header.
	 */
	def private generateHeader() '''
		--- generated by RoboCert sequences experiment
		--- on «timestamp»
	'''
	
	/**
	 * @return the current datetime as a human-readable timestamp.
	 */
	def private timestamp() {
		new SimpleDateFormat("dd-MM-yyyy HH:mm:ss").format(new Date())
	}
	

	/**
	 * @return included CSP for all preamble CSP groups.
	 * 
	 * @param resource  the top-level property model.
	 */
	private def generatePreambleCSPGroups(Resource resource) '''
		«FOR it : resource.allContents.filter(CSPGroup).filter[isPreamble].toIterable»
			«generate»
		«ENDFOR»
	'''

	/**
	 * @return included CSP for all groups.
	 * 
	 * @param resource  the top-level property model.
	 */
	private def generateGroups(Resource resource) '''
		«FOR it : resource.allContents.filter(Group).toIterable»
			«generateGroup»
		«ENDFOR»
	'''
	
	def private dispatch generateGroup(CSPGroup it) '''
		«IF !isPreamble»«generate»«ENDIF»
	'''
	
	def private dispatch generateGroup(SequenceGroup it) {
		sg.generateGroup(it)
	}
	
	def private dispatch generateGroup(Group it) {
		unsupported("group", "")
	}

	//
	// Assertions
	//
	/**
	 * @return generated CSP for all assertions.
	 * 
	 * @param resource  the top-level property model.
	 */
	private def generateAssertions(Resource resource) '''
		«FOR asst : resource.allContents.filter(Assertion).toIterable»
			«asst.generate»
		«ENDFOR»
	'''
}