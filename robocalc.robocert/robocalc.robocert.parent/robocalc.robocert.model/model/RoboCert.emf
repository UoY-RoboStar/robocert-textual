@namespace(uri="http://robocert.robocalc/", prefix="robocert")
@genmodel
	( basePackage="robocalc.robocert.model"
	, modelPluginID="robocalc.robocert.model"
	, modelDirectory="/robocalc.robocert.model/src-gen"
	, editDirectory="/robocalc.robocert.model.edit/src-gen"
	, editorDirectory="/robocalc.robocert.model.editor/src-gen"
	, testsDirectory="/robocalc.robocert.model.tests/src-gen"
	)
package robocert;

import "platform:/resource/circus.robocalc.robochart/model/robochart.ecore";

///////////////////////////////////////////////////////////////////////////////
// UTILITIES
///////////////////////////////////////////////////////////////////////////////

/**
 * An element that might be named, but might not be.
 *
 * This is usually useful for elements where the name adds some syntactic
 * complexity but enables cross-reference, and so we want to offer a possible
 * unnamed variant.
 */
abstract class OptionallyNamedElement {
	/**
	 * The name (can be null).
	 */
	attr String[0..1] name;
}

///////////////////////////////////////////////////////////////////////////////
// CORE LANGUAGE
///////////////////////////////////////////////////////////////////////////////

@genmodel(documentation="A RoboCert package.")
class RAPackage extends robochart.BasicPackage {
	@genmodel(documentation="Any groups in this package.")
	!ordered val Group[*]#parent groups;
	
	@genmodel(documentation="Any assertions in this package.")
	val Assertion[*] assertions;
}

@genmodel(documentation="A group in a RoboCert package.

Groups contain specifications in a particular RoboCert notation.")
abstract class Group {
	@genmodel(documentation="The parent package.")
	ref RAPackage[1]#groups parent;
}

@genmodel(documentation="An assertion.")
class Assertion extends robochart.NamedElement {
	@genmodel(documentation="The root property of the assertion.")
	val Property[1] property;
}

@genmodel(documentation="A property in an assertion.")
abstract class Property {
	// TODO(@MattWindsor91): we may eventually want conjunctions/disjunctions,
	// in which case I might need to re-evaluate whether isNegated belongs here.

	@genmodel(documentation="Whether the assertion must fail to hold for the model to be validated.")
	attr boolean[1] isNegated;
}

///////////////////////////////////////////////////////////////////////////////
// CSP
///////////////////////////////////////////////////////////////////////////////

@genmodel(documentation="A group of raw CSP-M declarations.

We generally interpret CSP-M as tock-CSP and implicitly generate it into a
timed section, but this can be overridden.")
class CSPGroup extends Group {
	@genmodel(documentation="If true, the group should be generated outside of a timed section.")
	attr boolean[1] isUntimed;
	
	@genmodel(documentation="If true, the group should be generated before any model-specific imports.
	
This is useful primarily for debugging purposes.

The group may be generated after RoboCert prelude imports, as these contain
the timing model depended upon by the tock-CSP encoding.")
	attr boolean[1] isPreamble;
	
	@genmodel(documentation="The fragments contained in this group.")
	val CSPFragment[*]#group fragments;
}

@genmodel(documentation="Abstract class of anything that can be interpreted as a CSP process.

This is predominantly a marker, and doesn't hold any state of its own.")
abstract class CSPProcessSource {
}

//
// CSP fragments
//

@genmodel(documentation="A fragment of CSP-M.

We generally interpret CSP-M as tock-CSP and implicitly generate it into a
timed section, but this can be overridden.")
abstract class CSPFragment {
	@genmodel(documentation="The raw contents of the CSP fragment.")
	attr String[1] contents;
}

@genmodel(documentation="A fragment of CSP-M that has a name.

These are used to bind CSP-M to names that are referred-to elsewhere.")
class NamedCSPFragment extends CSPFragment, robochart.NamedElement {
}

@genmodel(documentation="A fragment of CSP-M that is bound to an event set.")
class EventSetCSPFragment extends NamedCSPFragment {
}

@genmodel(documentation="A fragment of CSP-M that is bound to a process.")
class ProcessCSPFragment extends NamedCSPFragment, CSPProcessSource {
	@genmodel(documentation="The set of all events that the process can communicate.

This is used for tick-tock model shifting.  We cannot, and do not,
check to make sure that this event set is an accurate representation
of the process's actual event universe.")
	ref EventSetCSPFragment[0..1] events;
}

@genmodel(documentation="A fragment of CSP-M that is not bound to anything.

Such fragments can be named, but don't need to be, and nothing in the semantics
will depend on such a name existing.")
class InlineCSPFragment extends CSPFragment, OptionallyNamedElement {
}

//
// CSP refinement
//

@genmodel(documentation="A property that requires a low-level CSP refinement relation.")
class CSPRefinementProperty extends Property {
	@genmodel(documentation="Left-hand side of the refinement.")
	ref CSPProcessSource[1] lhs;

	@genmodel(documentation="Left-hand side of the refinement.")
	ref CSPProcessSource[1] rhs;

	@genmodel(documentation="Type of assertion.")
	attr CSPRefinementOperator[1] type;
	
	@genmodel(documentation="Model under which this assertion should be checked under CSP.")
	attr CSPModel[1] model = "TICK_TOCK";	
}

@genmodel(documentation="A tock-CSP refinement operator.")
enum CSPRefinementOperator {
	@genmodel(documentation="LHS refines RHS.")
	REFINES;

	@genmodel(documentation="LHS refines RHS and RHS refines LHS.")
	EQUALS;
}

@genmodel(documentation="A tock-CSP refinement model.")
enum CSPModel {
	@genmodel(documentation="Tick-tock refinement model.")
	TICK_TOCK;
	
	@genmodel(documentation="Traces refinement model (with prioritisation).")
	TRACES;
}

//
// SEQUENCES
//

/**
 * A collection of sequence diagrams between a given target and world.
 */
class SequenceGroup extends Group, robochart.NamedElement {
	/**
	 * The target actor of the sequence group.
	 */
	val Target[1]#group target;
	/**
	 * The world actor of the sequence group.
	 */
	val World[1]#group world;
	/**
	 * Any message sets named on the sequence group.
	 */
	val NamedMessageSet[*]#group messageSets;	
	/**
	 * The sequences inside this sequence group.
	 */
	val Sequence[*]#group sequences;
	/**
	 * Picks up both actors of the sequence diagram.
	 */
	derived volatile transient readonly ref Actor[2] actors;
}

@genmodel(documentation="A sequence diagram.")
class Sequence extends robochart.NamedElement, CSPProcessSource {
	/**
	 * The group to which this sequence belongs.
	 */
	ref SequenceGroup[1]#sequences group;
	/**
	 * The top-level subsequence of the sequence diagram.
	 */
	val Subsequence[1] body;

	//
	// Derived properties
	//

	/**
	 * Picks up the target of this sequence diagram.
	 */
	derived volatile transient readonly ref Target[1] target;
	/**
	 * Picks up the world of this sequence diagram.
	 */
	derived volatile transient readonly ref World[1] world;
	/**
	 * Picks up both actors of the sequence diagram.
	 */
	derived volatile transient readonly ref Actor[2] actors;
}

/**
 * A sequence of steps, forming part or all of a sequence diagram.
 */
class Subsequence {
	/**
	 * The steps contained within the subsequence.
	 * There is always at least one step.
	 */
	val SequenceStep[+]#parent steps;
	
	/**
	 * The parent sequence.
	 */
	derived volatile transient readonly ref Sequence[0..1] sequence;
}

/**
 * A single step in a subsequence.
 */
abstract class SequenceStep {
	/**
	 * The parent subsequence of this step.
	 */
	ref Subsequence[1]#steps parent;
}

/**
 * A sequence step containing a primitive action.
 */
class ActionStep extends SequenceStep {
	/**
	 * The specification of what can happen in the 'gap' before the action.
	 */
	val MessageSet[1] gap;

	/**
	 * The action proper.
	 */
	val SequenceAction[1]#step action;
}

/**
 * A defined and named message set.
 */
class NamedMessageSet extends robochart.NamedElement {
	/**
	 * The sequence group on which this message set is defined.
	 */
	ref SequenceGroup#messageSets group;

	/**
	 * The inner message set.
	 */
	val MessageSet[1] set;
}

/**
 * A set of gap message specifications.
 */
abstract class MessageSet {
	/**
	 * Whether this message set has messages.
	 *
	 * For instance, universal message sets are always active, but
	 * extensional ones may be if no messages are held,
	 * reference sets are active iff their referred-to sets are,
	 * and binary message sets depend on the operator and operands.
	 *
	 * This is an optimistic estimate, and may falsely report activity
	 * when there isn't any.
	 */
	derived volatile readonly transient attr boolean active;
	
	/**
	 * Whether this message set contains every message.
	 *
	 * Universal message sets are always universal,
	 * reference sets are active iff their referred-to sets are,
	 * and binary message sets depend on the operator and operands.
	 *
	 * This is a pessimistic estimate, and may falsely deny universality
	 * when there is some.
	 */
	derived volatile readonly transient attr boolean universal;	
}

/**
 * A message set that is, itself, a reference to a previously named message
 * set.
 */
class RefMessageSet extends MessageSet {
	/**
	 * The message set being named.
	 */
	ref NamedMessageSet[1] set;
}

/**
 * A message set that extensionally enumerates allowed or forbidden messages.
 */
class ExtensionalMessageSet extends MessageSet {
	/**
	 * The message specifications held by this message set.
	 */
	!ordered val MessageSpec[*]#parent messages;
}

/**
 * A message set that includes all messages.
 */
class UniverseMessageSet extends MessageSet {
}

/**
 * A message set that is the result of applying a binary operator to two other
 * message sets.
 */
class BinaryMessageSet extends MessageSet {
	/**
	 * The left-hand side.
	 */
	val MessageSet[1] lhs;
	/**
	 * The set operator.
	 */
	attr BinarySetOperator[1] operator;
	/**
	 * The right-hand side.
	 */
	val MessageSet[1] rhs;
}

/**
 * A binary set operator.
 */
enum BinarySetOperator {
	/**
	 * Set union ('or').
	 */
	UNION;
	/**
	 * Set intersection ('and').
	 */
	INTERSECTION;
	/**
	 * Set difference ('except').
	 */
	DIFFERENCE;
}

//
// ACTORS
//

/**
 * An actor in a sequence.
 *
 * There are two actors in a sequence: the 'target' of the sequence,
 * which is the system under test; and the 'world', which is the sum of
 * everything connected to the target.  They are bound to names used in the
 * diagram itself, but the target may also name an external RoboChart entity.
 */
abstract class Actor {
	@genmodel(documentation="Typically expands to the target's sequence group parameter.")
	derived volatile transient readonly ref SequenceGroup[0..1] anyGroup;
}

/**
 * The world of a sequence.
 */
class World extends Actor {
	/**
	 * The sequence group to which this actor is attached.
	 */
	ref SequenceGroup[1]#world group;
}

@genmodel(documentation="A target of some specification.

Targets are CSP process sources, through appeal to the existing RoboStar
language semantics.")
abstract class Target extends Actor, CSPProcessSource {
	/**
	 * The sequence group to which this actor is attached.
	 */
	ref SequenceGroup[1]#target group;
	
	/**
	 * An initial instantiation to be applied to the target.
	 * Assertions may apply a further instantiation.
	 */
	val TargetInstantiation[1] instantiation;
	
	@genmodel(documentation="Lowest common denominator for the targeted element.")
	derived volatile transient readonly ref robochart.NamedElement[1] element;	
}

/**
 * A sequence target referring to a RoboChart module.
 */
class RCModuleTarget extends Target {
	ref robochart.RCModule[1] module;
}

/**
 * Specifies a (partial) instantiation of a target.
 */
class TargetInstantiation {
	/**
	 * Any constant assignments.
	 */
	val ConstAssignment[*] assignments;
}

/**
 * Assigns a value to one or more constants.
 */
class ConstAssignment {
	/**
	 * The constant(s), being assigned.
	 */
	ref robochart.Variable[1..*] constants;
	/**
	 * The expression containing the new value of the constant(s).
	 */
	val robochart.Expression[1] value;
}

//
// CONTROL FLOW
//

/**
 * A sequence step that contains exactly one body.
 *
 * Often, but not always, these will be (what UML would call) combined
 * fragments.
 */
abstract class StepWithBody extends SequenceStep {
	/**
	 * The body of the action.
	 */
	val Subsequence[1] body;
}

/**
 * A step containing one or more sequence elements alongside a deadline
 * for all of those elements to terminate, expressed in time units.
 */
class DeadlineStep extends StepWithBody {
	/**
	 * The deadline, expressed as a number of time units after the start of the
	 * block.
	 */
	val robochart.Expression[1] units;
}

/**
 * A step containing one or more sequence elements in loop.
 *
 * A loop action may have a name, which is useful when specifying break actions.
 */
class LoopStep extends StepWithBody, OptionallyNamedElement {
	/**
	 * The loop bound.
	 */
	val LoopBound[1] bound;

	// TODO: conditionals
	// TODO: break
}

/**
 * A bound on the number of iterations the loop can make.
 */
abstract class LoopBound {
}

/**
 * A loop bound that makes the loop execute infinitely until otherwise
 * broken.
 */
class InfiniteLoopBound extends LoopBound {
}

/**
 * A loop bound that executes a specific number of times.
 *
 * Corresponds to UML2 loop(times).
 */
class DefiniteLoopBound extends LoopBound {
	/**
	 * The number of times the loop must execute, unless prematurely
	 * broken.
	 * Evaluated once only.
	 */
 	val robochart.Expression[1] times;
}

/**
 * A loop bound that executes a nondeterministic number of times that
 * is at least a lower bound.
 *
 * Corresponds to UML2 loop(lowerTimes, *).
 */
class LowerLoopBound extends LoopBound {
	/**
	 * The number of times the loop must execute at least, unless
	 * prematurely broken.
	 * Evaluated once only.
	 */
 	val robochart.Expression[1] lowerTimes;
}

/**
 * A loop bound that executes a nondeterministic number of times between
 * two bounds.
 *
 * Corresponds to UML2 loop(lowerTimes, upperTimes).
 */
class RangeLoopBound extends LoopBound {
	/**
	 * The lower number of times the loop must execute, unless
	 * prematurely broken.
	 * Evaluated once only.	 
	 */
	val robochart.Expression[1] lowerTimes;
	/**
	 * The upper number of times the loop must execute.
	 * Evaluated once only.
	 */
	val robochart.Expression[1] upperTimes;
}

//
// ACTIONS
//

/**
 * An action in a sequence diagram.
 */
abstract class SequenceAction {
	/**
	 * The step containing this action.
	 */
	ref ActionStep[1]#action step;
}

/**
 * A communicating action in a sequence diagram.
 */
class ArrowAction extends SequenceAction {
	/**
	 * The body of the arrow.
	 */
	val MessageSpec[1] body;
}

/**
 * A special action marking the end of a diagram.
 *
 * This exists mainly to allow a sequence gap at the end of the diagram.
 */
class FinalAction extends SequenceAction {
}

/**
 * An action representing a wait for some amount of time units.
 */
class WaitAction extends SequenceAction {
	/**
	 * The number of units to wait.
	 */
	val robochart.Expression[1] units;
	
	// TODO(@MattWindsor91): nondeterministic waits
}

//
// MESSAGES
//

/**
 * A specification of a message.
 */
class MessageSpec {
	/**
	 * The topic of the message.
	 */
	val MessageTopic[1]#spec topic;
	
	/**
	 * The direction of the message.
	 */
	attr MessageDirection[1] direction = "OUTBOUND";
	
	/**
	 * The argument list for the message.
	 */
	val Argument[*] arguments;
	
	//
	// Derived references
	//
	// Most of these come from walking backwards up a MessageSpec to its
	// enclosing SequenceGroup, and won't work if a MessageSpec isn't
	// contained in one.
	//
	
	/**
	 * The target of the message.
	 *
	 * If the message is outbound, this will be the 'from' of the message;
	 * else, the 'to'.
	 */
	derived transient readonly volatile ref Target[1] target;
	
	/**
	 * The source of the message.
	 */
	derived transient readonly volatile ref Actor[1] from;
	/**
	 * The destination of the message.
	 */
	derived transient readonly volatile ref Actor[1] to;
}

/**
 * A message topic.
 */
abstract class MessageTopic {
	/**
	 * The parent message specification.
	 */
	ref MessageSpec[1]#topic spec;
}

/**
 * An event message topic.
 */
class EventTopic extends MessageTopic {
	/**
	 * The name of the event.
	 */
	ref robochart.Event[1] event;
}

/**
 * An operation message topic.
 */
class OperationTopic extends MessageTopic {
	/**
	 * The name of the operation.
	 */
	ref robochart.OperationSig[1] operation;
// TODO: check whether this should be Operation instead
// TODO: parameters
}

/**
 * The direction of a message.
 */
enum MessageDirection {
	/**
	 * Heading in from world to target.
	 */
	INBOUND;
	/**
	 * Heading out from target to world.
	 */
	OUTBOUND;
}

/**
 * An argument pattern.
 */
abstract class Argument {
}

/**
 * An argument pattern specifying an expression.
 */
class ExpressionArgument extends Argument {
	/**
	 * The expression in question.
	 */
	val robochart.Expression[1] expr;
}

/**
 * An argument pattern specifying that all following arguments are unbound.
 */ 
class RestArgument extends Argument {
}


@genmodel(documentation="A property of a sequence.")
class SequenceProperty extends Property {
	@genmodel(documentation="Sequence to which this assertion refers.")
	ref Sequence[1] sequence;

	@genmodel(documentation="Type of assertion.")
	attr SequencePropertyType[1] type;
	
	@genmodel(documentation="Assertion-level instantiation applied to this sequence's target.")
	val TargetInstantiation[1] instantiation;
	
	@genmodel(documentation="Model under which this assertion should be checked under CSP.")
	attr CSPModel[1] model = "TICK_TOCK";	
}

@genmodel(documentation="Type of sequence assertion.")
enum SequencePropertyType {
	@genmodel(documentation="All behaviours of the target are included in the sequence.")
	HOLDS;

	@genmodel(documentation="All behaviours of the sequence are observable on the target.")
	IS_OBSERVED;
}
